/**
 * File:
 *   etc_gshadow.scr
 * Summary:
 *   SCR Agent for reading /etc/gshadow
 * Access:
 *   read-write
 * Authors:
 *   Unknown <yast2-hacker@suse.de>
 * See:
 *   anyagent
 *   libscr
 * Example:
 *   Read(.etc.gshadow)
 *   ([$["groupname":"user", "password":"*", "disposer":"root",
 * "userlist":"buch,lxuser", ],
 *     ... 
 *   ])
 * 
 * $Id$
 *
 * Returns a <b>list of maps</b>. Each map (list-entry) corresponds 
 * with one group. 
 * Keys for the maps are: "groupname", "password", "disposer", "userlist". 
 */
.etc.gshadow

`ag_anyagent(
  SCR::AnyAgent (
      ``(File("/etc/gshadow")),	// real filename
      "#",			// Comment
      false,
      ``(List (
	Or (
	  Tuple (
	  `groupname (String("")),
          ":",
          `password (String("")),
          ":",
	  `disposer (String("")),
	  ":",
          `userlist (String(""))	  
	),

	Tuple (
	  `groupname (Or (String("^:"), "")),
	  ":",
	  `password ( Or (String ("^:"), "") ),
	  ":",
	  `disposer (Or (String ("^:"), "")),
	  ":",
	  `userlist (Or (String ("^\n"), ""))
	)),
	"\n"
      ))
    )
)
