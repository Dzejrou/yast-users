/**
 * File:
 *   etc_group.scr
 * Summary:
 *   SCR Agent for reading /etc/group
 * Access:
 *   read-only
 * Authors:
 *   Unknown <yast2-hacker@suse.de>
 * See:
 *   anyagent
 *   libscr
 *   man group
 * Example:
 *   Read(.etc.group)
 *   ([$["gid":0, "groupname":"root", "password":"x", "userlist":"root"], 
 *     $["gid":1, "groupname":"bin", "password":"x", "userlist":"root,bin,daemon"], 
 *     $["gid":2, "groupname":"daemon", "password":"x", "userlist":""], 
 *     ...
 *   ])
 * 
 * $Id$
 *
 * Returns a <b>list of maps</b>. Each map (list-entry) corresponds to one
 * group.
 * Keys for the maps are: "groupname", "password", "gid" and "userlist".
 */
.etc.group

`ag_anyagent(
  SCR::AnyAgent (
      ``(File("/etc/group")),	// real filename
      "#",			// Comment
      true,
      ``(List (
	Or (
	  Tuple (
	  `groupname (String("+")),
	  ":",
	  `password (String ("")),
	  ":",
	  `gid (String ("")),
	  ":",
	  `userlist (String (""))
	),

	Tuple (
	  `groupname (Username()),
	  ":",
	  `password (Or (String ("^:"), "")),
	  ":",
	  `gid (Number ()),
	  ":",
	  `userlist (Or (String ("^\n"), ""))
	)),
	"\n"
      ))
    )
)
