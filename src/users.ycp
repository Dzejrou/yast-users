/**
 * File:
 *  users.ycp
 *
 * Module:
 *  Configuration of the users and groups
 *
 * Summary:
 *  Main file of users.
 *
 * Authors:
 *  Jan Holesovsky <kendy@suse.cz>, 2001
 *  Johannes Buchhold <jbuch@suse.de> 	
 *
 * $Id$
 *
 * Main file of yast2-config-users package. Calls all other modules.
 */

{
    /***
     * <H3>YaST2 Configuration of the users and groups</H3>
     */

    textdomain "users";
    import "Wizard";
    import "Console";
    import "Mode";

    y2milestone("mode %1", Mode::cont );
    
    // ===== arguments ================================================

    boolean test_mode = false;
    boolean start_sequencer_again = false;
    string  start_dialog = "users";
    integer arg = 0;

    
    while ( arg < size( Args() ) )
    {
	y2debug( "arg #%1: %2", arg, Args( arg ) );
	if ( Args( arg ) == .test )
	{
	    test_mode = true;
	}
	else
	{
	    start_dialog = Args( arg );
	}
	arg = arg + 1;
    }


    y2debug( "Starting with %1 - test mode: %2", start_dialog, test_mode );
    
    // ===== Definitions ==============================================
    
    // The most important variable
    map user_settings = $[];

    // For cloning and so
    map  current_users   = $[];
    map  current_groups  = $[];
    list current_gshadow = [];

    map group_in_work = $[];
    map user_in_work  = $[];

    // ===== global values=============================================
    string valid_logname_chars  = "0123456789abcdefghijklmnopqrstuvwxyz";
    string valid_password_chars = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ#* ,.;:._-+!$%&/|\?{[()]}";
    string valid_home_chars     = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ/";
    string valid_group_chars    = "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ";
    string valid_grouplist_chars= "0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ,";
    string valid_id_chars       = "0123456789";
    
   

    
    include "security/io.ycp";              
    map security  		= SecurityRead();
    integer max_uid             = tointeger(lookup( security, "UID_MAX" , 60000));
    integer max_gid             = tointeger(lookup( security, "GID_MAX" , 60000));
    integer max_system_uid      = tointeger(lookup( security, "UID_MIN" , 500)); 
    integer max_system_gid      = tointeger(lookup( security, "GID_MIN" , 100));
    integer max_length_uid      = size(sformat("%1", max_uid));
    integer max_length_gid      = size(sformat("%1", max_gid));
    integer max_length_pas 	= tointeger(lookup( security,"PASS_MAX_LEN",8));
    integer min_length_pas 	= tointeger(lookup( security,"PASS_MIN_LEN",5));
    
    
    if( test_mode )    SCR::Write(.dumpto.tmp.security, security  );
    
    max_system_gid = max_system_gid - 1;
    max_system_uid = max_system_uid - 1;
    string default_pw           = "******";
    integer focusline_group = 0;
    integer focusline_user  = 0;
    boolean view_all_users  = false;
    boolean view_all_groups = false;

    
    // ===== includes =================================================
    //include "ui/wizard_dialog.ycp";
    include "wizard/sequencer.ycp";
    
    include "users/ui.ycp";
    include "users/io.ycp";

   
    string language = UI::GetLanguage(true);
    string encoding = Console::SelectFont( language);
    
    usersGroupsShadowReadFromDisk();
    usersUsersReadFromDisk();
    usersGroupsReadFromDisk();
  
    list pam_list = SCR::Read(.pam.passwd.password.pam_unix);
    map  pam_map  = select( pam_list, 0, $[]);
    boolean pam   = contains( splitstring( lookup( pam_map , "arguments", ""), " "), "md5");

    map useradd_defaults  = SCR::Read(.etc.default.useradd);
    y2milestone(" uasd %1",useradd_defaults);


    y2milestone(" getGroup : %1", getDefaultGid());
    y2milestone(" getDefaultHome ; %1", getDefaultHome());
    y2milestone(" getDefaultShell : %1", getDefaultShell());
    
    // test pam -md5
    if( test_mode ) {
	y2milestone(" orgi pam : %1 ", pam );
	//pam = true;
	y2milestone(" cryptmd5 : %1 ", cryptmd5( "askldjfhakjsh"));
	y2milestone(" crypt    : %1 ",  crypt( "askldjfhakjsh"));
	SCR(`Write(.dumpto.tmp.pam, pam_list ));
    }

    if( pam )
    {
	max_length_pas = 128;
	min_length_pas = 8;
    }
    
    // if in inst-system and one user was already created switsch to overview
    if( start_dialog == "user_add" && ( Mode::cont ))
    {
	//y2milestone(" users  %1 ", current_users );
	map non_system_user = filter(`key, `user, current_users, ``( ( user["uid"]:0 >   max_system_uid) && user["username"]:"" != "nobody"  ));
	if( size( non_system_user ) > 0 )
	{
	    start_dialog = "users";
	}
    }
    
    // ===== Open UI ==================================================
    if( ! Mode::cont )
    {
	Wizard::CreateDialog();
    }
	
    
    // ===== Data for the WizardSequencer =============================
    map aliases = $[
	"users":         ``(usersUsersDialog()),
        "user_add":      ``(usersEditUser(`add_user)),
        "user_clone":    ``(usersEditUser(`clone_user)),
        "user_edit":     ``(usersEditUser(`edit_user)),
	"user_details":  ``(usersEditUserDetails()),
        "user_delete":   ``(usersDeleteUser()),
	"user_save":     ``(usersUserSaveToMap()),
	
	"groups":        ``(usersGroupsDialog()),
        "group_add":     ``(usersEditGroup(`add_group)),
        "group_clone":   ``(usersEditGroup(`clone_group)),
        "group_edit":    ``(usersEditGroup(`edit_group)),
	//"group_details": ``(usersEditGroupDetails()),
        "group_delete":  ``(usersDeleteGroup()),
	"group_save"  :  ``(usersGroupSaveToMap()),

	"without_save" :  ``(usersReallyAbort()),
	"save_to_system": ``(usersSaveDialog())

    ];

    map main_sequence = $[
        "ws_start":      start_dialog,
        "users":         $[ `switch_groups:  "groups",
			    `switch_users:   "users",
			    `new:            "user_add",
		            `clone:          "user_clone",
		            `edit:           "user_edit",
		            `delete:         "user_delete",
		            `next:           "save_to_system",
			    `abort:          `abort,
			    `cancel:         "without_save",
			    `exit:           `abort ],
	"user_add":      $[ `next:           "user_save",
	      		    `details:        "user_details",
			    `abort:          `abort,
			    `cancel:         "without_save",
			    `back:           "users" ],
	"user_clone":    $[ `next:           "user_save",
			    `details:        "user_details",
			    `cancel:         "without_save",
	                    `abort:          "without_save" ],
	"user_details":  $[ `abort:          "without_save",
			    `add_user:       "user_add",
			    `cancel:         "without_save",
			    `clone_user:     "user_clone",
  			    `edit_user:      "user_edit"],
	"user_edit":     $[ `next:           "user_save",
			    `details:        "user_details",
			    `cancel:         "without_save",
	                    `abort:          "without_save" ],
	"user_delete":   $[ `next:           "users",
	                    `abort:          `abort ],
	"user_save":     $[ `next:	     "users",
			    `save:           "save_to_system"],
        "groups":        $[ `switch_users:   "users",
			    `switch_groups:  "groups",
	                    `new:            "group_add",
		            `clone:          "group_clone",
			    `edit:           "group_edit",
		            `delete:         "group_delete",
		            `next:           "save_to_system",
		            `abort:          "without_save",
			    `cancel:         "without_save",
			    `exit:           `abort ],
	"group_add":     $[ `next:           "group_save",
			  //`details:        "group_details",
	                    `abort:          "without_save",
			    `cancel:         "without_save",
			    `back:           "groups" ],
	"group_clone":   $[ `next:           "group_save",
			  //`details:        "group_details",
			    `cancel:         "without_save",
	                    `abort:          "group_save" ],
	"group_edit":    $[ `next:           "group_save",
			  //`details:        "group_details",
			    `cancel:         "without_save",
	                    `abort:          "without_save" ],
	//"group_details": $[ `abort:          "without_save" ],
	"group_delete":  $[ `next:           "groups",
			    `abort:          "without_save" ],
	"group_save"  :  $[ `next:           "groups",
			    `save:           "save_to_system" ],
	"without_save":  $[ `next :          `next,
			    `back :          `back   ],
	"save_to_system": $[ `next: 	     `next ]
	
    ];

   
    
	
    // ===== Start the sequence =======================================
    
    any ret = WizardSequencer(aliases, main_sequence);

    if ( start_sequencer_again )
    {
	
	user_settings = $[];
	current_users   = $[];
	current_groups  = $[];
	current_gshadow = [];

	group_in_work = $[];
	user_in_work  = $[];

	usersGroupsShadowReadFromDisk();
	usersUsersReadFromDisk();
	usersGroupsReadFromDisk();
	start_dialog = "users";
	main_sequence["ws_start"]="users";
	ret = WizardSequencer(aliases, main_sequence);
    }

    if( ! Mode::cont )
	UI::CloseDialog();

    y2milestone( "Returning with %1", ret );
    return ret;
    
} // End
