/**
 * Module:		Users.ycp
 *
 * Authors:		Anas Nashif (nashif@suse.de)
 *
 * Purpose:		Handle users functions
 *
 * $Id$
 */
{

    module "Users";
    textdomain "users";

    import "Console";
    import "Mode";

    import "UserDefs";
    import "UserAddDefs";
    import "UserSettings";
    import "UserWriteStack";


    map user_in_work  = $[];

    global define void Users()``{

	UserAddDefs::Read();
	UserDefs::Read();
	UserSettings::ReadSettingsFromDisk();

    }

    /**
     * Get all the user configuration from a map.
     * When called by users_auto (preparing autoinstallation data)
     * the list can't be  empty.
     * @param settings	A list of users to be added to the system
     * @return	success
     */

    global define boolean Import (list settings) ``{
	foreach(`u , settings, ``{
	    if (u["username"]:"" != "root") {
		string def_home = UserAddDefs::GetDefaultHome() + u["username"]:"lxuser";
		string fullname =  u["forename"]:"forname" + " " + u["surname"]:"surname";

		integer gid = UserDefs::max_gid;
		// check the defaultgroup
		//y2milestone("groups %1", current_groups);

		gid = UserSettings::GetGid( "users" );

		string mod_password = "";
		if (! u["encrypted"]:false) {
		    mod_password = crypt( u["user_password"]:"");
		} else {
		    mod_password = u["user_password"]:"";
		}
		user_in_work = $[
				 "what"        :  `add_user ,
				 "fullname"    :  u["fullname"]:fullname,
				 "gid"         :  u["gid"]:gid,
				 "home"        :  u["home"]:def_home,
				 "password"    :  mod_password,
				 "shell"       :  u["shell"]:UserAddDefs::GetDefaultShell(),
				 "uid"         :  u["uid"]:UserSettings::NextFreeUid(),
				 "username"    :  u["username"]:"lx_user",
				 "grouplist"   :  u["grouplist"]:"dialout,uucp,video,audio",
				 "forename"    :  u["forename"]:"forname",
				 "surname"     :  u["surname"]:"surname"
		];

		any ret = UserWriteStack::UserSave(user_in_work , false );
	    }
	});
	return true;
    }

    /**
     * Saves users into the system
     * @param writeonly	 true for autoinstal - don't start the service yet
     * @return true on success
     */
    global define boolean Write ()``{
	any ret = UserWriteStack::SaveDialog(true);
	return true;
    }

}
